// Top-level build file where you can add configuration options common to all sub-projects/modules.
buildscript {
    ext.kotlin_version = '1.4.21'
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

repositories {
    mavenCentral()
    maven { url 'https://jitpack.io' }
}

apply plugin: 'kotlin'
apply plugin: 'maven'

group = 'com.github.ridi'
version = '1.1.1'

configurations {
    ktlint
}

dependencies {
    compile 'io.reactivex.rxjava2:rxjava:2.2.20'
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"

    testCompile 'junit:junit:4.12'

    ktlint 'com.pinterest:ktlint:0.34.2'
    ktlint 'com.github.ridi:ktlint-ruleset:master-SNAPSHOT'
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

artifacts {
    archives sourcesJar
}

task ktlint(type: JavaExec, group: 'verification') {
    description = 'Check Kotlin code style.'
    classpath = configurations.ktlint
    main = 'com.pinterest.ktlint.Main'
    args 'src/**/*.kt'
}

afterEvaluate { project ->
    check.dependsOn ktlint
}

task ktlintFormat(type: JavaExec, group: 'formatting') {
    description = 'Fix Kotlin code style deviations.'
    classpath = configurations.ktlint
    main = 'com.pinterest.ktlint.Main'
    args '-F', 'src/**/*.kt'
}

install {
    repositories.mavenInstaller {
        pom.project {
            licenses {
                license {
                    name 'The MIT License'
                    url 'https://opensource.org/licenses/MIT'
                    distribution 'repo'
                }
            }
        }
    }
}
